plugins {
    id 'java'
    id 'application'
    id 'org.beryx.jlink' version '2.26.0'
}

group 'space.typro'
version '1.0-SNAPSHOT'

sourceCompatibility = '21'
targetCompatibility = '21'

ext {
    javafxVersion = "21"
    javafxPlatform = System.getProperty("os.name").toLowerCase().contains("win") ? "win" :
            System.getProperty("os.name").toLowerCase().contains("mac") ? "mac" : "linux"
}

repositories {
    mavenCentral()
}

dependencies {
    // Основные модули JavaFX
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${javafxPlatform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${javafxPlatform}"
    implementation "org.openjfx:javafx-base:${javafxVersion}:${javafxPlatform}"
    implementation "org.openjfx:javafx-fxml:${javafxVersion}:${javafxPlatform}"
    implementation "org.openjfx:javafx-media:${javafxVersion}:${javafxPlatform}"
    implementation "org.openjfx:javafx-web:${javafxVersion}:${javafxPlatform}"

    implementation("ch.qos.logback:logback-classic:1.5.13")
    implementation("ch.qos.logback:logback-core:1.5.13")

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.15.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.15.0'

    compileOnly("org.projectlombok:lombok:1.18.30")
    annotationProcessor("org.projectlombok:lombok:1.18.30")

    implementation("org.slf4j:slf4j-api:2.0.9")

    implementation 'org.jetbrains:annotations:24.0.1'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0'

    implementation 'com.google.code.gson:gson:2.13.1'

    implementation project(':DirectoryManager')
    implementation project(':PacketManager')
    implementation project(':common')
}

application {
    mainClass = 'space.typro.typicallauncher.Main'
    applicationDefaultJvmArgs = [
            "--module-path", configurations.runtimeClasspath.asPath,
            "--add-modules", "javafx.controls,javafx.fxml,javafx.graphics,javafx.base,javafx.media,javafx.web"
    ]
}

jar {
    manifest {
        attributes 'Main-Class': 'space.typro.typicallauncher.Main'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
}

run {
    jvmArgs = [
            "--module-path", configurations.runtimeClasspath.asPath,
            "--add-modules", "javafx.controls,javafx.fxml,javafx.graphics,javafx.base,javafx.media,javafx.web"
    ]
}